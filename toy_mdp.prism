mdp

const int grid_size = 2;
const int no_bots = 2;
//const int no_tasks = 2;

module robot1

	R1Lock : bool; // lock for clock sync
	x1 : [0..grid_size]; // location variables
	y1 : [0.. grid_size];	

	[release] R1Lock = true -> (R1Lock'=false); // release lock according to the sync module


	// movement commands
	[R1MoveLeft] !R1Lock & x1>0 -> (x1'=x1-1) & (R1Lock' = true);
	[R1MoveRight] !R1Lock & x1<grid_size -> (x1'=x1+1) & (R1Lock' = true);
	[R1MoveUp] !R1Lock & y1<grid_size -> (y1'=y1+1) & (R1Lock' = true);
	[R1MoveDown] !R1Lock & y1>0 -> (y1'=y1-1) & (R1Lock' = true);

	//complete a task

	// wait
	[R1Wait] !R1Lock -> (R1Lock' = true);



endmodule


module robot2 = robot1[x1=x2, y1=y2, R1Lock=R2Lock, R1MoveLeft=R2MoveLeft, R1MoveRight=R2MoveRight,
			 R1MoveUp=R2MoveUp, R1MoveDown=R2MoveDown,R1Wait = R2Wait] endmodule


module task1

	T1Active : bool;
	tx1 : [0..grid_size];
	ty1 : [0..grid_size];

	[T1Complete] T1Active=true & x1=tx1 & y1=ty1 -> (T1Active' = false);
	[T1Complete] T1Active=true & x2=tx1 & y2=ty1 -> (T1Active' = false);
	
endmodule

module task2 = task1 [tx1=tx2, ty1=ty2, T1Active=T2Active, T1Complete=T2Complete] endmodule


module sync

	completed : [0..no_bots];
	
	[release] completed = no_bots -> (completed'=0);

	[R1MoveLeft] true -> (completed' = min(completed+1, no_bots));
	[R1MoveRight] true -> (completed' = min(completed+1, no_bots));
	[R1MoveUp] true -> (completed' = min(completed+1, no_bots));
	[R1MoveDown] true -> (completed' = min(completed+1, no_bots));
	[R1Wait] true -> (completed' = min(completed+1, no_bots));

	[R2MoveLeft] true -> (completed' = min(completed+1, no_bots));
	[R2MoveRight] true -> (completed' = min(completed+1, no_bots));
	[R2MoveUp] true -> (completed' = min(completed+1, no_bots));
	[R2MoveDown] true -> (completed' = min(completed+1, no_bots));
	[R2Wait] true -> (completed' = min(completed+1, no_bots));

endmodule

rewards "conflicts"
	
	x1=x2 & y1=y2 : 1;

endrewards

rewards "time"
		
	[release] true: 1;

endrewards

init
	x1 = 0 &
	y1 = 0 &
	x2 = 1 &
	y2 = 0 &
	R1Lock = false & 
	R2Lock = false &
	T1Active = true &
	T2Active = true &
	completed = 0
endinit
